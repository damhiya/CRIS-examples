From CRIS.common Require Import CRIS.
From CRIS.imp_system.imp Require Import ImpPrelude.
From CRIS.ring Require Import CellHeader CellA RingHeader RingA CtrlI.

Lemma mod_addL_app `{Σ : GRA} l l' : Mod.addL (l ++ l') = (Mod.addL l) ★ (Mod.addL l').
Proof using.
  induction l; s.
  - rewrite left_id. eauto.
  - rewrite -assoc. rewrite IHl. eauto.
Qed.

(* Simulation Proof *)
Module CtrlIA. Section CtrlIA.
  Context `{!crisG Γ Σ α β τ _S _I, _CELL: !cellGS}.

  Variable max_size : nat.

  Context (spt sps : specmap).

  (* Definitions of a list of Cell modules *)
  Local Definition CellA := (λ idx, CellA.t idx spt).
  Definition CellG start len : Mod.t := Mod.addL (List.map CellA (seq start len)).
  Definition CellGS := (CellG 0 max_size).

  (* Definitions of RingA module and RingI module *)
  Local Definition RingA := (RingA.t max_size sps).
  Local Definition CtrlI := (CtrlI.t max_size).
  Local Definition RingAMod := (RingA ★ CellGS).
  Local Definition RingIMod := (CtrlI ★ CellGS).

  (* Splits a cell group [CellG] ranging from [start] to [start+len-1] into three parts around an index [idx].
     Isolates a single cell [CellA idx] from other cells *)
  Lemma cellgroup_split idx start len (RANGE : start <= idx < start + len):
    CellG start len =
      (CellG start (idx-start)) ★ (CellA idx) ★
        (CellG (S idx) (start + len - idx - 1)).
  Proof.
    unfold CellG.
    assert (EQ : seq start len =
                seq start (idx-start) ++ seq idx (S (start + len - idx - 1))).
    { etrans; [|etrans]; cycle 1.
      - apply (seq_app (idx-start) (start + len - idx) start).
      - f_equal. f_equal; nia.
      - f_equal. nia.
    }
    rewrite EQ map_app mod_addL_app. eauto.
  Qed.

  Lemma big_sepL_mod {T} (φ : nat -> T -> iProp Σ) (l : list T):
     ([∗ list] i↦x ∈ l, φ (i mod List.length l) x) -∗
     ([∗ list] i↦x ∈ l, φ i x).
  Proof.
    iIntros "H". iApply (big_sepL_impl with "H").
    iModIntro. iIntros (? ?) "% H".
    eapply eq_ind; try iAssumption. f_equal.
    destruct (lookup_lt_is_Some l k).
    eauto using Nat.mod_small.
  Qed.

  Lemma mod_add_ex (a b c : nat)
    (NEQ : c ≠ 0)
    (EX : exists x, a = b + x * c):
    a mod c = b mod c.
  Proof. destruct EX. subst. eapply Nat.Div0.mod_add; eauto. Qed.

  Lemma big_sepL_rotate {T} (φ : nat -> T -> iProp Σ) n (l : list T):
    ([∗ list] i↦x ∈ l, φ ((n+i) mod List.length l) x) -∗
    ([∗ list] i↦x ∈ rotate (List.length l - n mod List.length l) l, φ i x).
  Proof.
    destruct (Nat.eq_decidable (List.length l) 0) as [|LENL].
    { destruct l; ss; iIntros "H"; iFrame. }
    iIntros "H". iApply big_sepL_mod. rewrite length_rotate.

    destruct (Nat.eq_decidable (n mod List.length l) 0) as [|LENN].
    { rewrite H Nat.sub_0_r.
      unfold rotate. rewrite Nat.Div0.mod_same; eauto.
      rewrite drop_0 take_0 app_nil_r.
      eapply eq_ind; try iAssumption. f_equal. extensionalities. f_equal.
      rewrite Nat.Div0.add_mod; eauto. rewrite H Nat.Div0.mod_mod; eauto.
    }
    assert (LE:= Nat.mod_upper_bound n _ LENL).

    iApply big_sepL_app. rewrite length_drop.
    rewrite Nat.mod_small; try nia.
    iPoseProof ((big_sepL_take_drop _ l (List.length l - n mod List.length l)) with "H") as "[H1 H2]".
    iSplitL "H2";
      (eapply eq_ind; try iAssumption; f_equal; extensionalities; f_equal).
    - eapply mod_add_ex; eauto.
      rewrite {1}(Nat.div_mod_eq n (List.length l)).
      exists (S (n / List.length l)). nia.
    - eapply mod_add_ex; eauto.
      rewrite {1}(Nat.div_mod_eq n (List.length l)).
      exists (n / List.length l). nia.
  Qed.

  Definition Ist (STATE : stateGS Σ) : iProp Σ :=
    (∃ (q q' : list Z) (hd tl : nat),
       RingA.v_que ↦src q↑ ∗ CtrlI.v_hd ↦tgt hd↑ ∗
       CtrlI.v_tl ↦tgt tl↑ ∗
       ⌜hd = (tl + List.length q)%nat /\ List.length (q ++ q') = max_size⌝ ∗
       ([∗ list] i↦x ∈ q, CellA.cell ((tl+i) mod max_size) x) ∗
       ([∗ list] i↦x ∈ q', (CellA.pending ((hd+i) mod max_size) ∨ CellA.cell ((hd+i) mod max_size) x)))%I.

  Notation IstFull :=
    (λ STATE, (Ist STATE ∗ IstEq CellGS STATE)%I).

  Lemma simF_init `{STATE : !stateGS Σ} :
    ⊢ ISim.sim_fun open RingAMod RingIMod IstFull (fid RingHdr.init).
  Proof using.
    cStartFunSim. rewrite /CtrlI.init /RingA.init.

    (* Simulation Starts Here *)
    (* SRC: precondition *)
    cStepsS. destruct Any.downcast; last (cStepsS; case_match; cStepsS; ss).
    cStepsS; cStepsT.
    iDestruct "IST" as "[IST CELLEQ]".
    iDestruct "IST" as (? ? ? ?) "(QUE & HD & TL & %INV & LIVE & FREE)".
    destruct INV as [HDREL LEN]. subst hd. cSimpl.

    (* TGT, SRC: take cSteps *)
    cStepsT. cStepsS. cPutS "QUE". cPutT "HD".
    cStepsT. cPutT "TL". cStepsT. cStepsS.
    cStep. iSplitL "". { eauto. }

    (* Prove the IST *)
    iSplitR "CELLEQ"; last iFrame.
    iExists [], (rotate (max_size - tl mod max_size) (q++q')%list), 0, 0.
    iFrame "QUE HD TL".
    iSplit.
    { iPureIntro. esplits; eauto. s. rewrite length_rotate. eauto. }

    iSplit; eauto. rewrite -LEN.
    iApply big_sepL_rotate. iApply big_sepL_app.
    iSplitL "LIVE".
    + iApply (big_sepL_impl with "LIVE").
      iModIntro. iIntros (k x) "% LIVE". iRight. s.
      rewrite Nat.Div0.mod_mod; eauto.
    + iApply (big_sepL_impl with "FREE").
      iModIntro. iIntros (k x) "% FREE". s.
      rewrite Nat.add_assoc.
      rewrite Nat.Div0.mod_mod; eauto.
  (*SLOW*)Qed.

  Lemma simF_get_size `{STATE : !stateGS Σ} :
    ⊢ ISim.sim_fun open RingAMod RingIMod IstFull (fid RingHdr.get_size).
  Proof using.
    cStartFunSim. rewrite /CtrlI.get_size /RingA.get_size.

    (* Simulation Starts Here *)
    (* SRC: precondition *)
    cStepsS. destruct Any.downcast; last (cStepsS; case_match; cStepsS; ss).
    cStepsS; cStepsT.
    iDestruct "IST" as "[IST CELLEQ]".
    iDestruct "IST" as (? ? ? ?) "(QUE & HD & TL & %INV & LIVE & FREE)".
    destruct INV as [HDREL LEN]. subst hd. cSimpl.

    (* TGT, SRC: take cSteps *)
    cStepsT. cStepsS. cGetS "QUE". cGetT "HD".
    cStepsT. cGetT "TL". cStepsT. cStepsS.
    cStep. iSplitL "". { rewrite Nat.add_comm Nat.add_sub. eauto. }

    (* Prove the IST *)
    iSplitR "CELLEQ"; last iFrame.
    repeat iExists _. iFrame. eauto.
  (*SLOW*)Qed.

  Lemma simF_enqueue `{STATE : !stateGS Σ} :
    ⊢ ISim.sim_fun open RingAMod RingIMod IstFull (fid RingHdr.enqueue).
  Proof using.
    unfold RingAMod, RingIMod, CellGS.
    cStartFunSim. rewrite /CtrlI.enqueue /RingA.enqueue.

    (* Simulation Starts Here *)
    (* SRC: precondition *)
    cStepsS. destruct Any.downcast; last (cStepsS; case_match; cStepsS; ss).
    cStepsS; cStepsT.
    iDestruct "IST" as "[IST CELLEQ]".
    iDestruct "IST" as (? ? ? ?) "(QUE & HD & TL & %INV & LIVE & FREE)".
    destruct INV as [HDREL LEN]. subst hd.
    cGetS "QUE". cGetT "HD". cStepsT. cGetT "TL".
    cStepsS. cStepsT.
    rename q into v. rename q' into l.

    (* TGT: check the length of the queue *)
    rewrite Nat.add_sub'; des_ifs; cycle 1.
    { cStep. cStep. iSplitL ""; eauto.
      iSplitR "CELLEQ"; last iFrame.
      iExists v, l, (tl + List.length v), tl. iFrame. eauto. }

    (* SRC: take cSteps *)
    cPutS "QUE". cStepsS.

    apply Nat.ltb_lt in Heq. rewrite length_app in LEN.
    assert (UBND:= Nat.mod_upper_bound (tl + List.length v) max_size).
    revert WFS WFT.
    rewrite (@cellgroup_split ((tl+ List.length v) mod max_size)); try nia.
    i; move_aux.

    (* TGT: inline CellHdr.set *)
    cStepsT. cInlineT.
    destruct l; [ss; nia|].
    cForceT (_,_). cForcesT.
    iDestruct "FREE" as "(Q & FREE)".
    (* rewrite !Nat.add_0_l in NODUPFS NODUPFT WFS WFT. *)
    rewrite !Nat.add_0_r.
    iSplitL "Q".
    { iFrame. eauto. }

    (* TGT: take cSteps using GRT from set_spec *)
    cStepsT. iDestruct "GRT" as "(% & % & CELL)". subst.
    cStepsT. cPutT "HD". cStepsT. cStep.
    iSplitL ""; eauto.

    (* Prove the IST *)
    iSplitR "CELLEQ"; last iFrame.
    iExists (v++[z]), l, ((tl + List.length v)+1), tl.
    iFrame "QUE HD TL".
    iSplitL "".
    { iPureIntro. esplits; eauto.
      - rewrite length_app. s. nia.
      - rewrite !length_app. s. cSimpl. nia.
    }
    iSplitL "LIVE CELL".
    + iApply big_sepL_app. iFrame. s. rewrite Nat.add_0_r. eauto.
    + iApply (big_sepL_impl with "FREE").
      iModIntro. iIntros (k x FIND) "H".
      rewrite <-!Nat.add_assoc. eauto.
  (*SLOW*)Qed.

  Lemma simF_dequeue `{STATE : !stateGS Σ} :
    ⊢ ISim.sim_fun open RingAMod RingIMod IstFull (fid RingHdr.dequeue).
  Proof using.
    unfold RingAMod, RingIMod, CellGS.
    cStartFunSim. rewrite /CtrlI.dequeue /RingA.dequeue.

    (* Simulation Starts Here *)
    (* SRC: precondition *)
    cStepsS. destruct Any.downcast; last (cStepsS; case_match; cStepsS; ss).
    cStepsS; cStepsT.
    iDestruct "IST" as "[IST CELLEQ]".
    iDestruct "IST" as (? ? ? ?) "(QUE & HD & TL & %INV & LIVE & FREE)".
    destruct INV as [HDREL LEN]. subst hd.

    (* TGT: check the length of the queue *)
    cGetS "QUE". cGetT "HD". cStepsT. cGetT "TL".
    cStepsS. cStepsT.
    destruct q; ss.
    { rewrite Nat.add_0_r Nat.sub_diag. s. cStep. cStep. iSplitL ""; eauto.
      iSplitR "CELLEQ"; last iFrame.
      iExists [], q', tl, tl. iFrame. eauto. }
    replace (tl + S(List.length q) - tl) with (S(List.length q)) by nia. s.
    rewrite !length_app in LEN.

    (* SRC: take cSteps *)
    cPutS "QUE". cStepsS.
    assert (UBND:= Nat.mod_upper_bound tl max_size).
    revert WFS WFT.
    rewrite (@cellgroup_split (tl mod max_size)); try nia.
    i; move_aux.

    (* TGT: inline CellHdr.get *)
    cStepsT. cInlineT. cForcesT. iDestruct "LIVE" as "(Q & LIVE)".
    rewrite !Nat.add_0_r.
    iSplitL "Q". { iFrame. eauto. }

    (* TGT: take cSteps using GRT from get_spec *)
    cStepsT. iDestruct "GRT" as "(% & % & CELL)". subst. cSimpl.
    cStepsT. cPutT "TL". cStepsT. cForcesS. cStep.
    iSplitL ""; eauto.

    (* Prove the IST *)
    iSplitR "CELLEQ"; last iFrame.
    iExists q, (q'++[z]), (tl + S(List.length q)), (tl + 1).
    iFrame "QUE HD TL".
    iSplit.
    { iPureIntro. split; first nia.
      rewrite !length_app /= in LEN |- *. nia. }
    iSplitL "LIVE".
    + iApply (big_sepL_impl with "LIVE").
      iModIntro. iIntros (k x FIND) "H".
      replace (tl + 1 + k) with (S (tl + k)) by nia.
      rewrite -Nat.add_succ_r. iFrame.
    + iApply big_sepL_app. iFrame. s. iSplitR ""; eauto.
      iRight. erewrite <-mod_add_ex; eauto; try nia.
      exists 1. nia.
  (*SLOW*)Qed.

  Lemma sim :
    RingA.init_cond max_size ⊢ ISim.t open RingAMod RingIMod IstFull.
  Proof using.
    iIntros "INIT".
    iApply (ISim_reflR open RingA CtrlI CellGS Ist).
    - mod_tac.
    - set_unfold; naive_solver.
    - intros. mod_tac.
    - iIntros (STATE fn) "%Hfn".
      repeat rewrite Mod.dom_fnsems_add in Hfn.
      set_unfold in Hfn; des; subst.
      + iApply simF_init.
      + iApply simF_get_size.
      + iApply simF_enqueue.
      + iApply simF_dequeue.
    - iIntros (STATE) "SRC TGT".
      rewrite /state_init_src /state_init_tgt.
      iDestruct "SRC" as "[SRC _]". iDestruct "TGT" as "[TGT _]".
      assert (SRCEQ :
        state_slice (list_to_set (Mod.scopes RingA))
          (Mod.initial_st RingA) = {[RingA.v_que := ([] : list Z)↑]}).
      { rewrite /RingA /RingA.t /SMod.to_mod /RingA.smod
          /state_slice /live_state /=. vm_compute. reflexivity. }
      assert (TGTEQ :
        state_slice (list_to_set (Mod.scopes CtrlI))
          (Mod.initial_st CtrlI) =
          {[CtrlI.v_hd := (0 : nat)↑; CtrlI.v_tl := (0 : nat)↑]}).
      { rewrite /CtrlI /CtrlI.t /SMod.to_mod /CtrlI.smod
          /state_slice /live_state /=. vm_compute. reflexivity. }
      iEval (rewrite SRCEQ big_sepM_singleton) in "SRC".
      iEval (rewrite TGTEQ big_sepM_insert) in "TGT".
      iDestruct "TGT" as "[HD TL]".
      iEval (rewrite big_sepM_singleton) in "TL".
      iExists [], (replicate max_size 0%Z), 0, 0.
      iFrame "SRC HD TL". iSplitR "INIT".
      { iPureIntro. split; s; eauto. rewrite length_replicate //. }
      s. iSplitR; eauto.
      iApply (big_sepL_impl with "INIT").
      iModIntro. iIntros (? ? FIND) "P".
      iLeft. rewrite Nat.mod_small; eauto.
      eapply lookup_replicate_1. eauto.
  (*SLOW*)Qed.
End CtrlIA. End CtrlIA.
